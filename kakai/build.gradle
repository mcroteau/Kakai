plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'maven-publish'
    id 'java-library'
    id 'signing'
    id 'java'
    id 'jacoco'
}

group 'io.kakai'
version '1.06'

repositories {
    mavenCentral()
}

dependencies {
    implementation "com.h2database:h2:2.1.210"
    testImplementation "org.jacoco:org.jacoco.core:0.8.7"
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.8.2"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:5.8.2"
}

jacoco {
    toolVersion = "0.8.7"
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

task sourcesJar(type:Jar){
    dependsOn(JavaPlugin.CLASSES_TASK_NAME)
    classifier = "sources"
    from(sourceSets["main"].allSource)
    artifacts {
        add("archives", sourcesJar)
    }
}

task artifact{
    dependsOn(build)
    dependsOn(sourcesJar)
    dependsOn(javadocJar)
}

task coverage{
    description = "Runs the unit tests with coverage"
    dependsOn 'test'
    dependsOn 'jacocoTestReport'
}

test {
    useJUnitPlatform()
    testLogging {
        showStandardStreams = true
    }
}

shadowJar {
    archiveBaseName.set('kakai')
    archiveClassifier.set('')
    archiveVersion.set(project.version)
    dependencies {
        exclude(dependency("org.jacoco:org.jacoco.core:0.8.7"))
        exclude(dependency("org.junit.jupiter:junit-jupiter-api:5.8.2"))
        exclude(dependency("org.junit.jupiter:junit-jupiter-engine:5.8.2"))
    }
}

publishing {
    repositories {
        maven {
            name = "sonatype"
            setUrl("https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/")
            credentials {
                username = findProperty("ossh.user")
                password = findProperty("ossh.pass")
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            groupId = "io.kakai"
            artifactId = "kakai"
            version = project.version
            from(components["java"])
        }
        maven(MavenPublication) {
            artifact(sourcesJar)
            artifact(javadocJar)
            pom {
                name.set("Kakai")
                description.set("Kakai runnable server contains " +
                        "everything you need to create a full stack java application. " +
                        "Kakai handles dependency injection, http routing, data persistence " +
                        "and page binding much like the jsp spec.")
                url.set("http://kakai.io")
                licenses {
                    license {
                        name.set("MIT license")
                        url.set("https://opensource.org/licenses/MIT")
                    }
                }
                issueManagement {
                    system.set("Github")
                    url.set("https://github.com/mcroteau/kakai/issues")
                }
                scm {
                    connection.set("https://github.com/mcroteau/kakai.git")
                    url.set("https://github.com/mcroteau/kakai")
                }
                developers {
                    developer {
                        name.set("Mike Croteau")
                        email.set("croteau.mike@gmail.com")
                    }
                }
            }
        }
    }
}

signing {
    def id = findProperty("gpg.id")
    def key = findProperty("gpg.key")
    def pass = findProperty("gpg.pass")
    useInMemoryPgpKeys(id, key, pass)
    sign(publishing.publications)
}
